let cgi = require "~/cgi"
let sql = require "~/sql"

let db-interface = require "db-interface"

let handle-get = function project-id do
	cgi :headers do
		content-type "text/html"
	end
	
	cgi :html do
		template (require "header.html")
		form "submit-report.beryl" "post" do
			template "<input type='hidden' value='%0' name='project_id' readonly/>" project-id
			label "content-field" "Report"
			template "<textarea id='content-field' name='report-content' rows='20' cols='80'></textarea>"
			submit "Submit"
		end
	end
end

let handle-post = function db user-token project-id content do
	let ok = db-interface :submit-report db user-token project-id content
	cgi :headers do
		if ok do
			redirect "/user-projects.beryl#project-" cat: project-id
		end else do
			redirect "/auth-error.html"
		end
	end
end

let req-type = assert (getenv "REQUEST_METHOD")
let cookies = cgi :parse-cookies (getenv "HTTP_COOKIE")

if req-type == "GET" do
	let params = cgi :parse-url-parameters (getenv "QUERY_STRING")
	handle-get (parse-number (params :project_id) default: "")
end elseif req-type == "POST" do
	let params = cgi :parse-url-parameters (input read-all)
	
	let db = sql :open (db-interface :db-path)
	handle-post db (cookies :user-login) (parse-number (params :project_id) default: "") (params :report-content)
	sql :close db
end
