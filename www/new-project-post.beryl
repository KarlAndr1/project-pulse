let cgi = require "~/cgi"
let sql = require "~/sql"

let admin-auth = require "admin-auth"
let utils = require "utils"

let req-type = assert (getenv "REQUEST_METHOD")
let server-data = assert (getenv "LIGHTTPD_DATA_PATH")

let redirect-to = null

let freq-to-time = function freq-word do
	(struct
		"every-minute" 60
		"daily" 60 * 60 * 24
		"weekly" 60 * 60 * 24 * 7
		"fortnightly" 60 * 60 * 24 * 14
		"monthly" 60 * 60 * 24 * 30
		"manual" null
	) freq-word
end

let handle-post = function db do
	let data = cgi :parse-url-parameters (input read-all)
	
	let name = assert (data :project-name)
	
	let freq = assert (data :reporting-frequency)
	
	let freq-time = freq-to-time freq
	let start-date = utils :parse-date (data :start-date)
	let end-date = utils :parse-date (data :end-date)
	
	let current-time = invoke time
	
	if (start-date =<= end-date) and? (end-date =>= current-time) do
		db "INSERT INTO projects (name, report_frequency, start_date, end_date) VALUES (?1, ?2, ?3, ?4)" name freq-time start-date end-date
		let new-project-id = sql :get-last-insert-rowid db
		
		if freq-time == null do
			redirect-to = cat "/add-project-dates.beryl?id=" new-project-id
		end else do
			redirect-to = cat "/manage-project.beryl?id=" new-project-id
		end
	end else do
		redirect-to = "/new-project.html"
	end
end

if req-type == "POST" do
	let cookies = cgi :parse-cookies (getenv "HTTP_COOKIE")
	let admin-login = cookies :admin-login

	let db = sql :open (cat server-data "/database")
	if (admin-auth :auth-admin db admin-login) do
		handle-post db
	end else do
		redirect-to = "/auth-error.html"
	end
	sql :close db
end

cgi :headers do
	redirect redirect-to
end
