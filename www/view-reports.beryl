let cgi = require "~/cgi"
let sql = require "~/sql"

let admin-auth = require "admin-auth"
let projects = require "projects"
let utils = require "utils"

let server-data = assert (getenv "LIGHTTPD_DATA_PATH")

let cookies = cgi :parse-cookies (getenv "HTTP_COOKIE")
let req-type = assert (getenv "REQUEST_METHOD")
let params = cgi :parse-url-parameters (getenv "QUERY_STRING")

let display-content = function db project-id period do
	let unread-reports = projects :get-reports db project-id period 0
	let read-reports = projects :get-reports db project-id period 1
	
	let project = (db "SELECT name, report_frequency, start_date, end_date FROM projects WHERE id = ?1" project-id) 0
	
	# https://stackoverflow.com/questions/1247361/get-the-inverse-of-a-join
	let members-without-reports = db "
		SELECT * FROM (
			SELECT
				u.name, u.member_id, u.email, u.phone_number, r.id as rid
			FROM users as u
			JOIN project_member_bindings as m
			ON
				m.user_id = u.id AND
				m.project_id = ?1
			LEFT JOIN reports as r
			ON
				r.user_id = u.id AND
				r.period = ?2 AND
				r.project_id = ?1
		)
		WHERE
			rid is NULL
	" project-id period
	
	cgi :html do
		template (require "header.html")
		h 1 (cat "Reports for " (project :name) ", period " period)
		
		div do
			link "âž¥Back" (cat "manage-project.beryl?id=" project-id)
			
			p do
				if period =/= "1" do
					link "Previous period" (cat "view-reports.beryl?pid=" project-id "&period=" ((parse-number period) - 1))
					print-exactly " | "
				end
				link "Next period" (cat "view-reports.beryl?pid=" project-id "&period=" ((parse-number period) + 1))
			end
			
			p do
				template "%0 reports submitted" (sizeof unread-reports) + (sizeof read-reports)
			end
		end
		
		section do
			div do
				h 2 "Unread"
				for-in unread-reports with report do
					template "
						<p class='highlight clickable tooltip' id='report-%0' data-text='%1'>
							%2
							<a href='view-report.beryl?report-id=%0' class='link_fill'>View</a> 
						</p>
					" (report :id) (utils :format-time (report :time)) (report :name)
				end
			end
			
			div do
				h 2 "Read"
				for-in read-reports with report do
					template "
						<p class='clickable tooltip' id='report-%0' data-text='%1'>
							%2
							<a href='view-report.beryl?report-id=%0' class='link_fill'>View</a> 
						</p>
					" (report :id) (utils :format-time (report :time)) (report :name)
				end
			end
		end
		
		div do
			h 2 "Unsubmitted"
			for-in members-without-reports with member do
				template "<p class='red'>"
				
					template "%0 (%1)" (member :name) (member :member_id)
					if (member :email) =/= null do
						template ", <a href='mailto:%0' class='tooltip' data-text='%0'>email</a>" (member :email)
					end
					if (member :phone_number) =/= null do
						template ", <a href='tel:%0' class='tooltip' data-text='%0'>phone</a>" (member :phone_number)
					end
					
				template "</p>"
			end
		end
	end
end

if req-type == "GET" do
	let admin-login = cookies :admin-login
	let db = sql :open (cat server-data "/database")
	if (admin-auth :auth-admin db admin-login) do
		cgi :headers do
			invoke content-type-html
		end
		display-content db (params :pid) default: 0 (params :period) default: 0
	end else do
		cgi :headers do
			redirect "/auth-error.html"
		end
	end
	sql :close db
end
