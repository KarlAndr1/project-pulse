let cgi = require "~/cgi"
let sql = require "~/sql"

let db-interface = require "db-interface"

let handle-get = function user is-admin do
	cgi :headers do
		invoke content-type-html
	end

	cgi :html do
		
		template (require "header.html")
		
		h 1 "Project Pulse"
		
		div do
			if is-admin do
				p "Logged in as administrator"
				p do
					link "Add users" "add-users.html"
					print-exactly " | "
					link "New Project" "new-project.html"
					print-exactly " | "
					link "View & Manage Projects" "view-projects.beryl"
					print-exactly " | "
					link "Log out" "admin-logout.html"
				end
			end elseif user =/= null do
				template "<p>Logged in as %0</p>" (user :name)
				p do
					link "View projects" "user-projects.beryl"
					print-exactly " | "
					link "Profile" "manage-user-profile.beryl"
					print-exactly " | "
					link "Log out" "user-logout.html"
				end
			end else do
				link "Login" "login.html"
			end
		end
	end
end

let cookies = cgi :parse-cookies (getenv "HTTP_COOKIE")
let req-type = assert (getenv "REQUEST_METHOD")

if req-type == "GET" do
	let db = sql :open (db-interface :db-path)
	let user = db-interface :auth-user db (cookies :user-login)
	let is-admin = db-interface :auth-admin db (cookies :admin-login)
	sql :close db
	
	handle-get user is-admin
end
