let cgi = require "~/cgi"
let sql = require "~/sql"

let db-interface = require "db-interface"

let redirect-to = null

let handle-post = function db admin-token params do
	let ok = null
	if :mark-read in? params do
		ok = db-interface "mark-report-read" db admin-token (params :report-id)
	end elseif :post-comment in? params do
		ok = db-interface "post-report-comment" db admin-token (params :report-id) (params :comment-content)
	end
	
	if ok do
		redirect-to = cat "/view-report.beryl?report-id=" (params :report-id)
	end else do
		redirect-to = "/auth-error.html"
	end
end

let req-type = assert (getenv "REQUEST_METHOD")
let cookies = cgi :parse-cookies (getenv "HTTP_COOKIE")

if req-type == "POST" do
	let params = cgi :parse-url-parameters (input read-all)
	
	let db = sql :open (db-interface :db-path)
	handle-post db (cookies :admin-login) params
	sql :close db
	
	cgi :headers do
		redirect (assert redirect-to)
	end
end
