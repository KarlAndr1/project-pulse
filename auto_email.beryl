let sql = require "~/sql"
let argon2 = require "~/argon2"

let db-interface = require "www/db-interface"
let auth = require "www/auth"
let utils = require "www/utils"
let mail-template = require "www/mail-templates/mail-template"

let db = sql :open (cat (getenv "LIGHTTPD_DATA_PATH") "/database")

let mail_password = strip (input read-all)

let months = (array
	"Jan"
	"Feb"
	"Mar"
	"Apr"
	"May"
	"Jun"
	"Jul"
	"Aug"
	"Sep"
	"Oct"
	"Nov"
	"Dec"
)

let format-date-time = function t do
	let two-digit = function num do
		if num < 10 do
			cat "0" num 
		end else do
			num
		end
	end
	
	let ts = convert-time t
	
	let year = ts :year
	let month = months (ts :month)
	let day = ts :day
	
	let hour = ts :hour
	let min = ts :minute
	let sec = ts :second
	
	cat day " " month " " year " " hour ":" min ":" sec " " "-0000"
end

let mail = function to to-name subject content do
	let url = getenv "AUTO_EMAIL_DOMAIN"
	let from = getenv "AUTO_EMAIL_USER"
	
	let mail-payload = (cat
		"From: Project Pulse" " <" from ">" carriage-return newline
		"To: " to-name " <" to ">" carriage-return newline
		"Date: " (format-date-time (invoke time)) carriage-return newline #"
		"Subject: " subject carriage-return newline #"
		carriage-return newline
		content
	)
	
	print "Sent mail to" to
	
	run "./send_mail.sh" url from to pass-input (cat mail_password newline mail-payload)
end

# ------------------ New users, assigning and sending out random passwords

let users-without-passwords = db "SELECT id, member_id, name, email FROM users WHERE encoded_password IS NULL"

let new-user-email-template = require "www/mail-templates/new-user.template"

for-in users-without-passwords with user do

	let password = rand-hexs 16
	let enc_pass = auth :encode-password password
	db "UPDATE users SET encoded_password = ?1 WHERE id = ?2" enc_pass (user :id)

	if (user :email) =/= null do
		let email-content = mail-template new-user-email-template (struct
			:USER_ID (user :member_id)
			:USER_NAME (user :name)
			:PASSWORD password
		)
		mail (user :email) (user :name) "Account created" email-content
	end
end

# --------------------- Database email queue

let email-queue = db "SELECT id, to_name, to_email, subject, content FROM email_queue"

for-in email-queue with due-email do
	if (due-email :to_email) =/= null do
		mail (due-email :to_email) (due-email :to_name) (due-email :subject) (due-email :content)
	end
	db "DELETE FROM email_queue WHERE id = ?1" (due-email :id)
end

# ---------------------- Deadline reminder notifactions

let all-projects = db-interface :__get-all-projects db

let deadline-email-template = require "www/mail-templates/report-deadline-notification.template"

let send-deadline-emails-for-project = function db project members reporting-details do
	for-in members with member do
		let submitted-report = db-interface :__get-report-for db (project :id) (member :id) (reporting-details :period)
		if (submitted-report == null) and? ((member :email) =/= null) do # Send out to all members who have not submitted a report, and who have an email address
			let mail-content = mail-template deadline-email-template (struct
				:USER_NAME (member :name)
				:PROJECT (project :name)
				:TIME_LEFT (utils :format-time-diff (reporting-details :time-left))
				:SUBMIT_REPORT_URL (cat "submit-report.beryl?project_id=" (project :id))
				:DUE_DATE (utils :format-time (reporting-details :period-ends-at))
				:PERIOD (as-string (reporting-details :period))
			)
			mail (member :email) (member :name) "Report deadline notification" mail-content
		end
	end
end

for-in all-projects with project do
	let project-members = db-interface :__get-project-members db (project :id)
	let current-time = invoke time

	if (current-time =>= (project :start_date)) and? (current-time =<= (project :end_date)) do
		let reporting-details = db-interface :__get-project-report-info db project
		let period = reporting-details :period
		if (period =/= null) do
			#The notification counter counts the highest period that notifications have been sent out for.
			if period > (project :sent_notifications_count) and? ((reporting-details :time-left) =<= 86400) do
				db "UPDATE projects SET sent_notifications_count = ?1" period
				send-deadline-emails-for-project db project project-members reporting-details
			end
		end
	end
end
